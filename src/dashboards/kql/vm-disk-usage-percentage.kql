InsightsMetrics
| where Origin == "vm.azm.ms" and Namespace == "LogicalDisk" and Name == "FreeSpacePercentage"
//| where TimeGenerated >= ago(2h)
| where _ResourceId == tolower("{resourceId}")
| extend Disk=tostring(todynamic(Tags)["vm.azm.ms/mountId"])
| extend FreeSpacePercentage = Val
| extend UsedSpacePercentage = (100 - FreeSpacePercentage)
| order by TimeGenerated desc
| summarize UsedSpacePercentage=avg(UsedSpacePercentage), FreeSpacePercentage=avg(FreeSpacePercentage) by Disk
// join same table to get FreeSpace in GB
| join kind=inner
    (
        InsightsMetrics
        | where Namespace == "LogicalDisk" and Name == "FreeSpaceMB"
        //| where TimeGenerated >= ago(2h)
        | where _ResourceId == tolower("{resourceId}")
        | extend FreeSpaceGB = Val /1000
        | extend Disk=tostring(todynamic(Tags)["vm.azm.ms/mountId"])
        | order by TimeGenerated desc
        | summarize FreeSpaceGB=max(FreeSpaceGB) by Disk
    )
    on Disk
| extend TotalDiskSizeGB = FreeSpaceGB / (FreeSpacePercentage / 100)
| extend UsedSpaceGB = TotalDiskSizeGB - FreeSpaceGB
| summarize 
FreeSpacePercentage=round(max(FreeSpacePercentage), 2),
FreeSpaceGB=round(max(FreeSpaceGB), 2), 
UsedSpacePercentage=round(max(UsedSpacePercentage),0),
UsedSpaceGB=round(max(UsedSpaceGB), 2),
TotalDiskSizeGB = round(max(TotalDiskSizeGB), 2) by Disk