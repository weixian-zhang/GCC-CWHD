let distinctActivities = AzureActivity
| extend ResourceCategory = parse_json(Properties).resourceProviderValue
| extend ActionNamespace = split(parse_json(Authorization).action, "/")
| extend ActionType = toupper(ActionNamespace[array_length(ActionNamespace) -1])
| where
    ActionType in ("WRITE", "DELETE") and
    CategoryValue  == "Administrative" and 
    ResourceCategory !in ("MICROSOFT.RESOURCEHEALTH", "MICROSOFT.RESOURCES") and
    ActivityStatusValue == "Success"
    | where TimeGenerated >= $__timeFrom()
| distinct CorrelationId;


AzureActivity
| extend PrincipalType =  parse_json(parse_json(Authorization).evidence).principalType
| extend SignInAcctType = iif(PrincipalType == "", "User", PrincipalType)
| extend AppId = parse_json(Claims)["appid"] //show appid (client id) of registered app if type = ServicePrincipal
| extend RBACRole = parse_json(parse_json(Authorization).evidence).role
| extend SigninUPN = iif(SignInAcctType == "ServicePrincipal",AppId, parse_json(Claims)["http://schemas.xmlsoap.org/ws/2005/05/identity/claims/upn"])
| extend SigninName = iif(SignInAcctType == "ServicePrincipal",AppId, parse_json(Claims)["name"])
| extend CallerIP = CallerIpAddress
| extend ResourceCategory = parse_json(Properties).resourceProviderValue
| extend ResourceIDSplit = split(_ResourceId, "/")
| extend Resource = strcat(toupper(ResourceIDSplit[array_length(ResourceIDSplit)-2]), "/",
         toupper(ResourceIDSplit[array_length(ResourceIDSplit)-1]))
| extend ActionNamespace = split(parse_json(Authorization).action, "/")
| extend ActionType = toupper(ActionNamespace[array_length(ActionNamespace) -1])
| extend SubscriptionName = split(Hierarchy, '/')[-2]
| join (distinctActivities) on CorrelationId
| where
    ResourceCategory != ""
| project 
    DateTimeSG = datetime_local_to_utc(TimeGenerated, 'Asia/Singapore'),
    Subscription=SubscriptionName, 
    ResourceGroup, 
    Action=ActionType,
    Resource=strcat(tostring(parse_json(Properties).resourceProviderValue),'/', Resource), 
    AccountType=SignInAcctType,
    RBACRole, 
    SigninUPN, 
    SigninName